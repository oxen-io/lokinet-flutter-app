group "io.oxen.lokinet_lib"
version "1.0-SNAPSHOT"

buildscript {
	ext.kotlin_version = "1.3.50"
	repositories {
		google()
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "com.android.tools.build:gradle:3.5.0"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.diffplug.spotless:spotless-plugin-gradle:5.14.0"
	}
}

rootProject.allprojects {
	repositories {
		google()
		jcenter()
	}
}
apply plugin: "com.diffplug.spotless"
apply plugin: "com.android.library"
apply plugin: "kotlin-android"

spotless {
	java {
		target "$projectDir/src/main/java/**/*.java"
		importOrder() // standard import order
		removeUnusedImports()
		googleJavaFormat()
		prettier()
	}
	groovyGradle {
		importOrder()
		target '*.gradle' // default target of groovyGradle
		greclipse()
	}

	kotlin {
		target "$projectDir/src/main/kotlin/**/*.kt"
		ktfmt()    // has its own section below
	}
}

android {
	compileSdkVersion 29

	ndkVersion '21.3.6528147'
	sourceSets {
		main.java.srcDirs += "src/main/java"
		main.java.srcDirs += "src/main/kotlin"
		main.jniLibs.srcDirs += "src/main/jniLibs"
	}
	defaultConfig {
		minSdkVersion 16
	}
	lintOptions {
		disable "InvalidPackage"
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task cleanNativeLibs(type: Delete) {
	delete "$projectDir/../external/lokinet/build-android"
}

task compileNativeLibs(type: Exec) {
  def dir = new File("$projectDir/../external/lokinet")
  if ( dir.exists() ) {
    workingDir "$projectDir/../external/lokinet"
    environment "NDK", project.android.ndkDirectory
    executable "./contrib/android.sh"
  }
}

task ensureNativeLibs(type: Copy) {
	from("$projectDir/../external/lokinet/") {
		include 'lokinet-jni-*/*/*.so'
		eachFile { fcd ->
			fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
		}
	}
	into "$projectDir/src/main/jniLibs/"
}

ensureNativeLibs.dependsOn compileNativeLibs
preBuild.dependsOn ensureNativeLibs
clean.dependsOn cleanNativeLibs
